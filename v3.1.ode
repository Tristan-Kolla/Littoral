# --------------------------- Helper Functions --------------------------------

# Functional Forms
maturation(phi,j) = (phi*j)/(1+j*j)
type3(h,p,f,kappa) = (h*p*f*f)/(1+kappa*f*f)
logistic(r,b,f) = r*f - b*f*f
sig(a) = 1/(1+exp(-a))
interact(a,x,y) = a*x*y
type3ff(h,kappa,f) = (h*f*f)/(1+kappa*f*f)

# Fitness 
fitPL(f,p) = type3ff(hL,kappaL,f) - omegap*p
fitPP(f,p) = type3ff(hP,kappaP,f) - omegap*p
fitFL(f,p,j) = (rL - bL*f) - type3(hL, p, f, kappaL) - omegaf*f
fitFP(f,p,j) = (rP - bP*f) - type3(hP, p, f, kappaP) - omegaf*f
fitJL(p,f,j) =  (phiL/(1 + j*j)) - kL*f - cL*p - omegaj*j
fitJP(p,f,j) =  (phiP/(1 + j*j)) - kP*f - cP*p - omegaj*j

# Movement
emig(d,toggle,alpha,win,wout)= d*(sig(toggle*alpha*(wout - win)))

# -------------------------- Governing Equations ------------------------------

# Adult Predator - littoral
PL' = maturation(phiL,JL) - (mP+mu)*PL \
      - emig(dP,toggle,alphaP, fitPL(FL,PL), fitPP(FP,PP))*PL \
      + emig(dP,toggle,alphaP, fitPP(FP,PP), fitPL(FL,PL))*PP
	  
# Forager - littoral
FL' = logistic(rL,bL,FL) - type3(hL,PL,FL,kappaL) + interact(epsL,FL,JL) \
      - emig(dF,toggle,alphaF, fitFL(FL,PL,JL), fitFP(FP,PP,JP))*FL \
      + emig(dF,toggle,alphaF, fitFP(FP,PP,JP), fitFL(FL,PL,JL))*FP
	  
# juvenile - littoral
JL' = fecL*PL - maturation(phiL,JL) - mJ*JL \
      - interact(kL,FL,JL) - interact(cL,PL,JL) \
      - emig(dJ,toggle,alphaJ, fitJL(PL,FL,JL), fitJP(PP,FP,JP))*JL \
      + emig(dJ,toggle,alphaJ, fitJP(PP,FP,JP), fitJL(PL,FL,JL))*JP
	  
# Adult Predator - pelagic
PP' = maturation(phiP,JP) - (mP+mu)*PP \
      - emig(dP,toggle,alphaP, fitPP(FP,PP), fitPL(FL,PL))*PP \
      + emig(dP,toggle,alphaP, fitPL(FL,PL), fitPP(FP,PP))*PL

# Forager - pelagic
FP' = logistic(rP,bP,FP) - type3(hP,PP,FP,kappaP) + interact(epsP,FP,JP) \
	  - emig(dF,toggle,alphaF, fitFP(FP,PP,JP), fitFL(FL,PL,JL))*FP \
      + emig(dF,toggle,alphaF, fitFL(FL,PL,JL), fitFP(FP,PP,JP))*FL

# Juvenile- pelagic
JP' = fecP*PP - maturation(phiP,JP) - mJ*JP \
      - interact(kP,FP,JP) - interact(cP,PP,JP) \
      - emig(dJ,toggle,alphaJ, fitJP(PP,FP,JP), fitJL(PL,FL,JL))*JP \
      + emig(dJ,toggle,alphaJ, fitJL(PL,FL,JL), fitJP(PP,FP,JP))*JL

# -------------------------- Parameters ---------------------------------------

par mu=0
par mP=0.100, mJ=0.100
par rL=0.400, bL=0.060, hL=1.500, kappaL=0.005,
par rP=0.200, bP=0.060, hP=1.500, kappaP=0.005 
par epsL=0.100, epsP=0.100
par fecL=0.8, fecP=0.8
par phiL=1.000, phiP=1.000
par kL=1.2, kP=1.2, cL=0.02, cP=0.02

par dP=0.1, dF=0.1, dJ=0.1
par alphaP=0,25, alphaF=0.25, alphaJ=0.25 
par omegap=0.01, omegaf=0.01, omegaj=0.01

par toggle = 1

# -------------------------- Initial conditions -------------------------------

init PL=5, FL=5, JL=5
init PP=5, FP=5, JP=5

@ total=1000, dt=0.1, bounds=1000

# ---------------------------plotting variables -------------------------------

@ XP=FL, YP=PL, XLO=0.1, XHIGH=8, YLOW=-0.1, YHIGH=5

# ------------------------------- Auto options --------------------------------

@ DS=0.0005, DSMIN=0.0001, DSMAX=0.05, NMAX=500 
@ PARMIN=-0.25, PARMAX=0.5, NORMMAX=20
@ AUTOXMIN=-0.2, AUTOXMAX=0.5, AUTOYMIN=-0.1, AUTOYMAX=5

#
