

# Helper ---------------------------------------------------------------------------

maturation(phi,j)= (phi*j)/(1+j*j)
type3(h,p,f,kappa)= (h*p*f*f)/(1+kappa*f*f)
logistic(r,b,f)= r*f - b*f*f
sig(a)= 1/(1+exp(-a))

fitPL(f)= (hL*f*f)/(1+kappaL*f*f) - omegap
fitPP(f)= (hP*f*f)/(1+kappaP*f*f) - omegap
fitFL(f,p)= (rL - bL*f) - (hL*p*f*f)/(1+kappaL*f*f) - omegaf
fitFP(f,p)= (rP - bP*f) - (hP*p*f*f)/(1+kappaP*f*f) - omegaf
fitJL(p,f)= (fecL*p) - mJ - kL*f - cL*p - omegaj
fitJP(p,f)= (fecP*p) - mJ - kP*f - cP*p - omegaj

emig(d,beta,alpha,win,wout)= d*(sig(beta*alpha*(wout - win)))

# Equations ------------------------------------------------------------------------

PL' = maturation(phiL,JL) - (mP+mu)*PL \
      - emig(dP,betaP,alphaP, fitPL(FL), fitPP(FP))*PL \
      + emig(dP,betaP,alphaP, fitPP(FP), fitPL(FL))*PP

FL' = logistic(rL,bL,FL) - type3(hL,PL,FL,kappaL) + epsL*FL*JL \
      - emig(dF,betaF,alphaF, fitFL(FL,PL), fitFP(FP,PP))*FL \
      + emig(dF,betaF,alphaF, fitFP(FP,PP), fitFL(FL,PL))*FP

JL' = fecL*PL - maturation(phiL,JL) - mJ*JL - kL*FL*JL - cL*PL*JL \
      - emig(dJ,betaJ,alphaJ, fitJL(PL,FL), fitJP(PP,FP))*JL \
      + emig(dJ,betaJ,alphaJ, fitJP(PP,FP), fitJL(PL,FL))*JP

PP' = maturation(phiP,JP) - (mP+mu)*PP \
      - emig(dP,betaP,alphaP, fitPP(FP), fitPL(FL))*PP \
      + emig(dP,betaP,alphaP, fitPL(FL), fitPP(FP))*PL

FP' = logistic(rP,bP,FP) - type3(hP,PP,FP,kappaP) + epsP*FP*JP \
      - emig(dF,betaF,alphaF, fitFP(FP,PP), fitFL(FL,PL))*FP \
      + emig(dF,betaF,alphaF, fitFL(FL,PL), fitFP(FP,PP))*FL

JP' = fecP*PP - maturation(phiP,JP) - mJ*JP - kP*FP*JP - cP*PP*JP \
      - emig(dJ,betaJ,alphaJ, fitJP(PP,FP), fitJL(PL,FL))*JP \
      + emig(dJ,betaJ,alphaJ, fitJL(PL,FL), fitJP(PP,FP))*JL


# Parameters -----------------------------------------------------------------------

par mu=0
par mP=0.280, mJ=0.280
par rL=0.500, bL=0.050, hL=1.500, kappaL=0.050
par rP=0.500, bP=0.050, hP=1.500, kappaP=0.050
par epsL=0.100, epsP=0.100
par fecL=1.000, fecP=1.000
par phiL=1.000, phiP=1.000
par kL=1.000, kP=1.000, cL=0.017, cP=0.017

par dP=1, dF=1, dJ=1
par betaP=0.0, betaF=0.0, betaJ=0.0
par alphaP=5.0, alphaF=5.0, alphaJ=5.0
par omegap=0.05, omegaf=0.05, omegaj=0.05

# Initial conditions ---------------------------------------------------------------

init PL=1.0, FL=0.3, JL=0.8
init PP=1.0, FP=0.3, JP=0.8

@ total=1000, dt=0.1, bounds=1000

# plotting variables ---------------------------------------------------------------

@ XP=FL, YP=PL, XLO=0.1, XHIGH=8, YLOW=-0.1, YHIGH=5

# Auto options ---------------------------------------------------------------------

# Numeric settings
@ DS=0.0005, DSMIN=0.0001, DSMAX=0.05, NMAX=500 
@ PARMIN=-0.25, PARMAX=0.5, NORMMAX=20
@ AUTOXMIN=-0.2, AUTOXMAX=0.5, AUTOYMIN=-0.1, AUTOYMAX=5
